@BUILDING_BATCH_PROCESSING
	WHILE 1
        CALL SINGLE_DRAWLINE
        ALIGNMENT CENTER
        PRINTFORML
        PRINTFORML 欢迎来到建筑管理中心！尊敬的%ANAME(MASTER)%
        PRINTFORML 请选择你要进行的操作
        PRINTFORML
        CALL SINGLE_DRAWLINE

		PRINTBUTTON "[建筑统计]",1
		PRINTL
        PRINTL
		PRINTBUTTON "[清除重复建筑]",2
		PRINTL
        PRINTL
        PRINTBUTTON "[清除特定建筑]",3
		PRINTL
        PRINTL
		PRINTBUTTON "[清除所有建筑]",4
		PRINTL
        PRINTL
		PRINTBUTTON "[批量建造建筑]",5
		PRINTL
        PRINTL
		[IF_DEBUG]
			PRINTBUTTON "[DEBUG:批量生成随机建筑]",6
			PRINTL
		[ENDIF]
		PRINTBUTTON "[返回]",0
		PRINTL
        ALIGNMENT LEFT
		INPUT
		IF RESULT == 0
			RETURN
        ENDIF
		IF RESULT == 1
			CALL BUILDING_BATCH_PROCESSING_CNT
        ENDIF
		IF RESULT == 2
			CALL BUILDING_BATCH_PROCESSING_CLEAR_UNNECESSARY_BUILDING
        ENDIF
        IF RESULT == 3
			CALL BUILDING_BATCH_PROCESSING_CLEAR_GIVEN_BUILDING
        ENDIF
		IF RESULT == 4
			CALL BUILDING_BATCH_PROCESSING_CLEAR_ALL_BUILDING
        ENDIF
		IF RESULT == 5
			CALL BUILDING_BATCH_PROCESSING_BUILD_BUILDING
        ENDIF
		[IF_DEBUG]
			IF RESULT == 7
                CALL DEBUG_CREATE_BUILDING
            ENDIF
		[ENDIF]

	WEND
@BUILDING_BATCH_PROCESSING_CNT
	#DIM TMP_BUILDING_NUM,19
	#DIM CITY_ID
    VARSET TMP_BUILDING_NUM
	FOR CITY_ID,0,MAX_CITY
		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID)++
	NEXT
    CALL SINGLE_DRAWLINE
	FOR LOCAL,建造物_兵舎,建造物_培養槽+1
		PRINTFORM 《%GET_DEVELOPMENT_NAME(LOCAL)%》
		PRINTFORM %TOSTR_SPACE(10 - STRLENS(GET_DEVELOPMENT_NAME(LOCAL)))%
		PRINTFORML 的数量是{TMP_BUILDING_NUM:(LOCAL)}
	NEXT
    CALL SINGLE_DRAWLINE
	PRINTFORMW 统计完毕！
@BUILDING_BATCH_PROCESSING_CLEAR_UNNECESSARY_BUILDING
	#DIM TMP_BUILDING_NUM,19
	#DIM TMP_CITY_DANGER,MAX_CITY
	#DIM TMP_CITY_DANGER_NUM
	#DIM TMP_CITY_SAFE,MAX_CITY
	#DIM TMP_CITY_SAFE_NUM
	#DIM CITY_ID
	VARSET TMP_BUILDING_NUM
	VARSET TMP_CITY_DANGER
	VARSET TMP_CITY_DANGER_NUM
	VARSET TMP_CITY_SAFE
	VARSET TMP_CITY_SAFE_NUM
	PRINTFORML 将清除己方领土上多余的不可重复生效建筑/没用的建筑/非危险都市的弓櫓
	PRINTFORML 包括《研究所》、《大本营》、《忍者之里》、《大型仓库》、《税務署》、《大宴会場》、《清掃局》、《公衆便所》、《武芸塾》、《知略塾》、《戦略塾》
	PRINTFORML 优先清除威胁度较高都市的建筑
	CALL ASK_YN
	SIF RESULT == 1
		RETURN
	FOR CITY_ID,0,MAX_CITY
		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID)++
		IF GET_CITY_RISK(CITY_ID) >= 4
			TMP_CITY_DANGER:TMP_CITY_DANGER_NUM = CITY_ID
			TMP_CITY_DANGER_NUM++
		ELSE
			TMP_CITY_SAFE:TMP_CITY_SAFE_NUM = CITY_ID
			TMP_CITY_SAFE_NUM++
		ENDIF
	NEXT

	WHILE TMP_CITY_DANGER_NUM > 0
		TMP_CITY_DANGER_NUM--
		CITY_ID = TMP_CITY_DANGER:TMP_CITY_DANGER_NUM
		IF GROUPMATCH(CITY_DEVELOPMENT:CITY_ID,建造物_研究所,建造物_本拠地,建造物_忍びの里,建造物_集積所,建造物_税務署,建造物_大宴会場,建造物_清掃局,建造物_公衆便所,建造物_戦略塾,建造物_知略塾,建造物_武芸塾)
			IF TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID) > 1
				TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID) --
				PRINTFORML 拆除了%CITY_NAME:CITY_ID%的《%GET_DEVELOPMENT_NAME(CITY_DEVELOPMENT:CITY_ID)%》
				CITY_DEVELOPMENT:CITY_ID = 0
			ENDIF
        ELSEIF GROUPMATCH(CITY_DEVELOPMENT:CITY_ID,建造物_戦略塾,建造物_知略塾,建造物_武芸塾)
			TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID) --
			PRINTFORML 拆除了%CITY_NAME:CITY_ID%的%GET_DEVELOPMENT_NAME(CITY_DEVELOPMENT:CITY_ID)%
			CITY_DEVELOPMENT:CITY_ID = 0
		ENDIF
	WEND

	WHILE TMP_CITY_SAFE_NUM > 0
		TMP_CITY_SAFE_NUM--
		CITY_ID = TMP_CITY_SAFE:TMP_CITY_SAFE_NUM
		IF GROUPMATCH(CITY_DEVELOPMENT:CITY_ID,建造物_研究所,建造物_本拠地,建造物_忍びの里,建造物_集積所,建造物_税務署,建造物_大宴会場,建造物_清掃局,建造物_公衆便所)
			IF TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID) > 1
				TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID) --
				PRINTFORML 拆除了%CITY_NAME:CITY_ID%的%GET_DEVELOPMENT_NAME(CITY_DEVELOPMENT:CITY_ID)%
				CITY_DEVELOPMENT:CITY_ID = 0
			ENDIF
		ELSEIF GROUPMATCH(CITY_DEVELOPMENT:CITY_ID,建造物_弓櫓,建造物_戦略塾,建造物_知略塾,建造物_武芸塾)
			TMP_BUILDING_NUM:(CITY_DEVELOPMENT:CITY_ID) --
			PRINTFORML 拆除了%CITY_NAME:CITY_ID%的%GET_DEVELOPMENT_NAME(CITY_DEVELOPMENT:CITY_ID)%
			CITY_DEVELOPMENT:CITY_ID = 0
		ENDIF
	WEND
	PRINTFORMW 清理完毕！

@BUILDING_BATCH_PROCESSING_CLEAR_GIVEN_BUILDING
    #DIM CITY_ID
    WHILE 1
        PRINTFORML 请选择要清除的建筑
        PRINTBUTTON "[兵舎]",建造物_兵舎
        PRINTL
        PRINTBUTTON "[独占市場]",建造物_独占市場
        PRINTL
        PRINTBUTTON "[湯治場]",建造物_湯治場
        PRINTL
        PRINTBUTTON "[醸造所]",建造物_醸造所
        PRINTL
        PRINTBUTTON "[超级计算机]",建造物_スパコン
        PRINTL
        PRINTBUTTON "[培養槽]",建造物_培養槽
        PRINTL
        PRINTBUTTON "[取消]",0
        PRINTL
        INPUT
        IF RESULT == 0
            CLEARLINE 8
            RETURN
        ELSEIF GROUPMATCH(RESULT,建造物_兵舎,建造物_独占市場,建造物_湯治場,建造物_醸造所,建造物_スパコン,建造物_培養槽)
            CLEARLINE 8
            FOR CITY_ID,0,MAX_CITY
                SIF CITY_TYPE:CITY_ID == 1
                    CONTINUE
                SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
                    CONTINUE
                IF CITY_DEVELOPMENT:CITY_ID == RESULT
                    PRINTFORML 拆除了%CITY_NAME:CITY_ID%的%GET_DEVELOPMENT_NAME(CITY_DEVELOPMENT:CITY_ID)%
			        CITY_DEVELOPMENT:CITY_ID = 0
                ENDIF
            NEXT
            RETURN
            PRINTFORMW 清除完成！
        ELSE
            CLEARLINE 8
        ENDIF
    WEND
@BUILDING_BATCH_PROCESSING_CLEAR_ALL_BUILDING
	#DIM CITY_ID
	SETCOLOR カラー_警告
	PRINTFORML 警告！警告！警告！
	RESETCOLOR
	PRINTFORML 将清除己方领土上所有建筑
	PRINTFORML 这个过程是不可逆的且不会返还任何资金！
	SETCOLOR カラー_警告
	PRINTFORML 警告！警告！警告！
	CALL ASK_YN
	SIF RESULT == 1
		RETURN
	FOR CITY_ID,0,MAX_CITY
		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		CITY_DEVELOPMENT:CITY_ID = 0
	NEXT
	PRINTFORML 清理完毕！
	PRINTFORMW 如需撤回请及时读档！
@BUILDING_BATCH_PROCESSING_BUILD_BUILDING
	#DIM CITY_ID
	#DIM STOP_LINE
	#DIM COUNTRY_ID
	COUNTRY_ID = CFLAG:MASTER:所属
	PRINTFORML 请注意建造需要消耗3000国库资金
	PRINTFORML 当前国库资金为{MONEY:COUNTRY_ID}
	IF MONEY:COUNTRY_ID < 3000
		PRINTFORMW 国库里没有足够的钱进行任何建造(需要国庫3,000)
		RETURN
	ENDIF
	WHILE 1
		PRINTFORML 设置停止资金线
		PRINTFORML 资金小于设定值后将停止建造
		PRINTFORML 请输入（0~{MONEY:COUNTRY_ID}）的数值
		INPUT
		IF RESULT >= 0 && RESULT <= MONEY:COUNTRY_ID
			CLEARLINE 1
			STOP_LINE = RESULT
			PRINTFORML 停止资金线 = {RESULT}
			BREAK
		ELSE
			CLEARLINE 4
		ENDIF
	WEND
	PRINTFORML 请选择需要建造的建筑
	PRINTBUTTON "[兵舎]",建造物_兵舎
	PRINTL
	PRINTBUTTON "[独占市場]",建造物_独占市場
	PRINTL
	RESETCOLOR
	PRINTBUTTON "[返回]",0
	PRINTL
	INPUT
	SIF RESULT != 建造物_独占市場 && RESULT != 建造物_兵舎
		RETURN
	FOR CITY_ID,0,MAX_CITY
		SIF CITY_TYPE:CITY_ID == 1
			CONTINUE
		SIF CITY_OWNER:CITY_ID != CFLAG:MASTER:所属
			CONTINUE
		SIF CITY_DEVELOPMENT:CITY_ID != 0
			CONTINUE
		IF MONEY:COUNTRY_ID < 3000
			PRINTFORMW 国库里没有足够的钱进行任何建造(需要国庫3,000)
			RETURN
		ENDIF
		IF MONEY:COUNTRY_ID <= STOP_LINE
			PRINTFORMW 金钱消耗抵达设定的停止线({STOP_LINE})
			RETURN
		ENDIF
		CITY_DEVELOPMENT:CITY_ID = RESULT
		MONEY:COUNTRY_ID -= 3000
		PRINTFORML 在了%CITY_NAME:CITY_ID%建造了%GET_DEVELOPMENT_NAME(RESULT)%
	NEXT
    PRINTFORMW 建造完毕！

@DEBUG_CREATE_BUILDING
    #DIM CITY_ID
	FOR CITY_ID,0,MAX_CITY
		CITY_DEVELOPMENT:CITY_ID = RAND(1,19)
	NEXT
    PRINTFORMW 生成完毕！
